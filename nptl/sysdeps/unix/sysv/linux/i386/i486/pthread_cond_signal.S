/* Copyright (C) 2002, 2003 Free Software Foundation, Inc.
   This file is part of the GNU C Library.
   Contributed by Ulrich Drepper <drepper@redhat.com>, 2002.

   The GNU C Library is free software; you can redistribute it and/or
   modify it under the terms of the GNU Lesser General Public
   License as published by the Free Software Foundation; either
   version 2.1 of the License, or (at your option) any later version.

   The GNU C Library is distributed in the hope that it will be useful,
   but WITHOUT ANY WARRANTY; without even the implied warranty of
   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
   Lesser General Public License for more details.

   You should have received a copy of the GNU Lesser General Public
   License along with the GNU C Library; if not, write to the Free
   Software Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA
   02111-1307 USA.  */

#include <sysdep.h>
#include <shlib-compat.h>
#include <lowlevelcond.h>
#include <kernel-features.h>

#ifdef UP
# define LOCK
#else
# define LOCK lock
#endif

#define SYS_futex		240
#define FUTEX_WAIT		0
#define FUTEX_WAKE		1
#define FUTEX_REQUEUE		3

#define EINVAL			22


	.text

	/* int pthread_cond_signal (pthread_cond_t *cond) */
	.globl	__pthread_cond_signal
	.type	__pthread_cond_signal, @function
	.align	16
__pthread_cond_signal:

	pushl	%ebx
	pushl	%esi
	pushl	%edi

	movl	16(%esp), %edi

	/* Get internal lock.  */
	movl	$1, %eax
	LOCK
#if cond_lock == 0
	xaddl	%eax, (%edi)
#else
	xaddl	%eax, cond_lock(%edi)
#endif
	testl	%eax, %eax
	jne	1f

2:	leal	wakeup_seq(%edi), %ebx
	movl	total_seq+4(%edi), %eax
	movl	total_seq(%edi), %ecx
	cmpl	4(%ebx), %eax
#if cond_lock != 0
	leal	cond_lock(%edi), %edi
#endif
	ja	3f
	jb	4f
	cmpl	(%ebx), %ecx
	jbe	4f

	/* Bump the wakeup number.  */
3:	addl	$1, (%ebx)
	adcl	$0, 4(%ebx)

	/* Wake up one thread by moving it to the internal lock futex.  */
	movl	$FUTEX_REQUEUE, %ecx
	movl	$SYS_futex, %eax
	xorl	%edx, %edx
	movl	$1, %esi
	ENTER_KERNEL

#ifndef __ASSUME_FUTEX_REQUEUE
	cmpl	$-EINVAL, %eax
	je	7f
#endif

	/* If we moved a thread we in any case have to make the syscall.  */
	testl	%eax, %eax
	jne	5f

	/* Unlock.  Note that at this point %edi always points to
	   cond_lock.  */
4:	LOCK
	subl	$1, (%edi)
	jne	5f

6:	xorl	%eax, %eax
	popl	%edi
	popl	%esi
	popl	%ebx
	ret

	/* Initial locking failed.  */
1:
#if cond_lock == 0
	movl	%edi, %ecx
#else
	leal	cond_lock(%edi), %ecx
#endif
	call	__lll_mutex_lock_wait
	jmp	2b

	/* Unlock in loop requires wakeup.  */
5:	movl	%edi, %eax
	call	__lll_mutex_unlock_wake
	jmp	6b

#ifndef __ASSUME_FUTEX_REQUEUE
7:	/* The futex requeue functionality is not available.  */
	movl	$1, %edx
	movl	$FUTEX_WAKE, %ecx
	movl	$SYS_futex, %eax
	ENTER_KERNEL
	jmp	4b
#endif
	.size	__pthread_cond_signal, .-__pthread_cond_signal
versioned_symbol (libpthread, __pthread_cond_signal, pthread_cond_signal,
		  GLIBC_2_3_2)
